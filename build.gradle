plugins {
    id "com.github.ben-manes.versions" version "0.51.0"
}

wrapper {
    gradleVersion = '7.6.1'
}

allprojects {
    group = 'de.topobyte'
    version = '1.3.0'
}

// leaving this here for reference on how to modify
// a single module version
configure(project(':osm4j-core')) {
    version = '1.3.0'
}

ext.libraries = [
    project(':osm4j-core'),
    project(':osm4j-edit'),
    project(':osm4j-extra'),
    project(':osm4j-geometry'),
    project(':osm4j-utils'),
    project(':osm4j-incubating'),
    project(':osm4j-pbf'),
    project(':osm4j-pbf-full-runtime'),
    project(':osm4j-replication'),
    project(':osm4j-tbo'),
    project(':osm4j-testing'),
    project(':osm4j-xml')
]

configure(libraries) {
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
}

configure(subprojects.findAll {it !in libraries}) {
    apply plugin: 'java'
}

subprojects {
    task allDeps(type: DependencyReportTask) {}

    apply plugin: 'eclipse'
    apply plugin: 'maven-publish'

    repositories {
        maven {
            url 'https://mvn.topobyte.de'
        }
        maven {
            url 'https://mvn.slimjars.com'
        }
        mavenCentral()
    }

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    java {
        withSourcesJar()
    }

    def global = new File('info.pom');
    def local = new File(project.projectDir, 'info.pom');

    def pomInfo = new XmlSlurper().parse(global);
    def publish = !project.hasProperty('dontPublish')
    if (publish) {
        def pomInfoL = new XmlSlurper().parse(local);
        pomInfo.name = pomInfoL.name
        pomInfo.description = pomInfoL.description
    }

    if (publish) {
        publishing {
            publications {
                maven(MavenPublication) {
                    from components.java
                    pom {
                        name = "$pomInfo.name"
                        description = "$pomInfo.description"
                        url = "$pomInfo.url"
                        licenses {
                            license {
                                name = "$pomInfo.licenseName"
                                url = "$pomInfo.licenseUrl"
                                distribution = "$pomInfo.licenseDistribution"
                            }
                        }
                    }
                }
            }
        }
    }
}

subprojects {
    task showInterModuleDependencies {
        doLast {
            println "module '$project.name'"
            project.configurations.compile.resolvedConfiguration.resolvedArtifacts.each {
                if (it.id instanceof org.gradle.internal.component.local.model.PublishArtifactLocalArtifactMetadata) {
                    def component = it.id.componentIdentifier
                    if (component instanceof org.gradle.api.internal.artifacts.DefaultProjectComponentIdentifier) {
                        def path = component.projectPath
                        def name = path.substring(path.lastIndexOf(':') + 1)
                        println "     â†’ '$name'"
                    }
                }
            }
        }
    }
}

def isNonStable = { String version ->
  def nonStableKeyword = ['ALPHA', 'BETA'].any { it -> version.toUpperCase().contains(it) }
  return nonStableKeyword
}

tasks.named("dependencyUpdates").configure {
  rejectVersionIf {
    isNonStable(it.candidate.version)
  }
}
